plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.vanniktech.maven.publish'
}

group = "io.github.rroohit"
version = "4.0.0"

android {
    namespace 'com.image.cropview'
    compileSdk 35

    defaultConfig {
        minSdk 24
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        compose true
    }

    lint {
        targetSdk 35
    }
    testOptions {
        targetSdk 35
    }
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()

    coordinates("io.github.rroohit", "ImageCropView", "4.0.0")

    pom {
        name = "ImageCropView"
        description = "Jetpack Compose Image Crop View."
        inceptionYear = "2022"
        url = "https://github.com/rroohit/ImageCropView"

        licenses {
            license {
                name = "Apache-2.0"
                url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }

        developers {
            developer {
                id = "rroohit"
                name = "Rohit Chavan"
            }
        }

        scm {
            url = "https://github.com/rroohit/ImageCropView"
            connection = "scm:git:git://github.com/rroohit/ImageCropView.git"
            developerConnection = "scm:git:ssh://github.com/rroohit/ImageCropView.git"
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs +=
            ["-Xexplicit-api=strict",
             "-Xopt-in=androidx.compose.ui.ExperimentalComposeUiApi"]
}

dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation "androidx.compose.ui:ui:1.10.4"
    implementation 'androidx.compose.ui:ui-geometry:1.10.4'
    implementation "androidx.compose.foundation:foundation:1.10.4"
}